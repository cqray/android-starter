apply plugin: 'com.android.library'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        //noinspection OldTargetApi
        targetSdkVersion 30
        versionCode 58
        versionName createVersionName(versionCode)

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 强制限定版本依赖版本
    configurations.all {
        resolutionStrategy {
            force 'androidx.core:core-ktx:1.6.0'
            force 'androidx.core:core:1.6.0'
            force 'androidx.appcompat:appcompat:1.3.1'
            force "androidx.activity:activity:1.3.1"
            force "androidx.fragment:fragment:1.3.1"
        }
    }
}

dependencies {

    api fileTree(dir: "libs", include: ["*.jar"])
    //noinspection GradleDependency
    api "androidx.appcompat:appcompat:1.3.1"
    //noinspection GradleDependency
    api "com.google.android.material:material:1.2.1"
    //JetPack Lifecycle、LiveData、ViewModel扩展
    api "androidx.lifecycle:lifecycle-common-java8:2.4.0"
    // 常用工具类
    api "com.blankj:utilcodex:1.31.0"
    // 加载动画
    api "com.github.ybq:Android-SpinKit:1.4.0"
    // 刷新框架
    api "com.scwang.smart:refresh-layout-kernel:2.0.3"
    // 3.0.4之后版本需要Kotlin，暂不适配Kotlin。
    // noinspection GradleDependency
    api "com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4"
    // RxJava
    api "io.reactivex.rxjava3:rxjava:3.1.5"
    api "io.reactivex.rxjava3:rxandroid:3.0.0"
    // Json格式化
    api "com.google.code.gson:gson:2.8.8"
    // ButterKnife工具
    compileOnly "com.jakewharton:butterknife:10.2.3"
    // Lombok框架
    // noinspection AnnotationProcessorOnCompilePath
    compileOnly "org.projectlombok:lombok:1.18.20"
    annotationProcessor "org.projectlombok:lombok:1.18.20"
}

/**
 * 生成版本名
 */
static String createVersionName(int code) {
    int num1 = code / 100
    int num2 = (code - num1 * 100) / 10
    int num3 = code - num1 * 100 - num2 * 10
    return num1 + "." + num2 + "." + num3
}